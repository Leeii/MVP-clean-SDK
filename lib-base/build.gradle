apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'


android {
    compileSdkVersion rootProject.ext.android["sdkVersion"]
    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    lintOptions {
        abortOnError true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    api rootProject.ext.defDependencied["appcompat-v7"]
    api rootProject.ext.defDependencied["design"]
    api rootProject.ext.defDependencied["cardview"]
    api rootProject.ext.defDependencied["recyclerview"]
    api rootProject.ext.defDependencied["constraint-layout"]

    api rootProject.ext.defDependencied["rxJava"]
    api rootProject.ext.defDependencied["rxAndroid"]
    api rootProject.ext.defDependencied["rxPermissions"]
    api rootProject.ext.defDependencied["rxLifecyle"]
    api rootProject.ext.defDependencied["rxLifecyle-compact"]

    api rootProject.ext.defDependencied["retrofit"]
    api rootProject.ext.defDependencied["adapter-rxjava2"]

    api rootProject.ext.defDependencied["fast-json"]

    api rootProject.ext.defDependencied["butterknife"]

    api rootProject.ext.defDependencied["dagger"]

    // sdk core
    api 'com.leeiidesu:sdk-core:1.1.0'
}
repositories {
    mavenCentral()
}

// 进行数字签名
signing {
    required { false }
    sign configurations.archives
}

// type显示指定任务类型或任务, 这里指定要执行Javadoc这个task,这个task在gradle中已经定义
task androidJavadocs(type: Javadoc) {
    // 设置源码所在的位置
    source = android.sourceSets.main.java.sourceFiles
}

// 生成javadoc.jar
task androidJavadocsJar(type: Jar) {
    // 指定文档名称
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

// 生成sources.jar
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

// 产生相关配置文件的任务
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

tasks.create('uploadSDKBase', Upload.class) {
    configuration = project.configurations.archives
    group = 'upload'
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'http://118.24.71.189/nexus/content/repositories/releases/') {
            authentication(userName: "${username}", password: "${password}")
        }
        pom.version = '2.1.12'
        pom.artifactId = 'sdk-base'
        pom.groupId = 'com.leeiidesu'
        pom.name = project.name
        pom.packaging = "aar"
    }
}